'   ****************************************************** 
'   *** Space Race - The AMOS Version
'   *** inspired by the Space Race console at the  
'       Arcade Archive at Retro Collective in Stroud, UK 

'   *** Written in  AMOS by Yawning Angel, 2025  

'   *** This is an attempt to get as close as possible to the  
'   *** arcade cabinet version at the Arcade Archive. It's not 
'   *** like for like, but close enough... maybe ;)

'   *** You WILL need the AMCAF extension installed in order to use this 
'   *** as it is.

'       Use of this code is at your own risk.
'       Yawning Angel is not responsible for any damage caused 
'       to your hardware or software if you use this code. 

'       There may be more efficient ways to use this code, and 
'       what is supplied here is not given as the best solution, 
'       merely a way of demonstrating functionality. 

'       If you get use out of this, consider making a donation 
'       on my ko-fi account: ko-fi.com/yawningangel
'       Thank you. :)

'       If you wish to change this program, please make a copy and 
'       make changes to that.  

'       Feel free to share your high scores with me on social  
'       media. Links available from my website.  

'       Enjoy! 

'       Yawning Angel 2025 
'       https://www.ya-retro.co.uk 
'       YouTube: @YawningAngelRetro

'   ****************************************************** 


' Define screen  
Screen Open 1,320,200,8,Lowres
Curs Off : Hide : Cls 0 : 

' Get bobs 
Load "SR_Bobs.abk"

' Do Bob stuff 
Get Bob Palette 
Double Buffer 
Make Mask 

' Define variables/globals etc.

' Arrays for holding meteor bob details
Dim L_METEOR(13),L_X_POS(13),L_Y_POS(13)
Dim R_METEOR(13),R_X_POS(13),R_Y_POS(13)

'*** Bobs 3 to 16 = left to right bobs 
For C=1 To 13
   L_METEOR(C)=C+2 : Rem Bob number 
   L_X_POS(C)=Rnd(300) : Rem X position of Bob
   L_Y_POS(C)=Rnd(160) : Rem Y position of Bob
Next C

' *** Bobs 17 to 30 = right to left bobs 
For CC=1 To 13
   R_METEOR(CC)=CC+16 : Rem Bob number 
   R_X_POS(CC)=Rnd(300) : Rem X position of Bob
   R_Y_POS(CC)=Rnd(160) : Rem Y position of Bob
Next CC


' Define values for player one ship
P1_SHIP_X_POS=80
P1_SHIP_Y_POS=170
P1_SCORE=0

' Define values for player two ship
P2_SHIP_X_POS=240
P2_SHIP_Y_POS=170
P2_SCORE=0


SHIP_V_SPEED=2
SHIP_I_SPEED=1


TIM_Y_POS=0

Global M_X_POS,M_Y_POS,M_SPEED,M#,B_HIT,NUM_MET
Global M_R_X,XX,TIM_Y_POS

Global L_METEOR()
Global L_X_POS()
Global L_Y_POS()

Global R_METEOR()
Global R_X_POS()
Global R_Y_POS()


M_X_POS=0
M_Y_POS=10
M_SPEED=1

XX=0

Paper 0 : Pen 2

' Set the screen font (AMCAF)
NF$="HCT.font"

' Display 'timer bar' in middle of screen  
For Y=0 To 200 Step 50
   
   Paste Bob 160,Y,5
   
Next Y

' This is the 'blob' that moves down the bar 
Bob 32,160,0,6


' Display '0' scores 
Ink 2,0
Change Font NF$,20
SCORE_DISPLAY["0","0"]


' Set curent time at start of loop 
' This is using AMCAF's 'Current Time' command 
CT1=Current Time
'Locate 0,0 : Print Ct Time$(CT1)
CT1_S=Ct Second(CT1)
CT1_M=Ct Minute(CT1)

SEC_COUNTER=0 : Rem counter for number of 'seconds' counted


' Main program loop
Do 
   
   ' Player 1 
   Bob 1,P1_SHIP_X_POS,P1_SHIP_Y_POS,7
   
   ' Player 2 
   Bob 2,P2_SHIP_X_POS,P2_SHIP_Y_POS,7
   
   
   ' Handle meteors scrolling across the screen 
   METEORS
   
   Wait Vbl 
   
   ' Check for out of time
   If TIM_Y_POS>=195
      GAME_OVER
   End If 
   
   ' ... collision detection? 
   P2#=Bob Col(2)
   If P2#=-1
      Boom 
      P2_SHIP_Y_POS=170
   End If 
   
   P1#=Bob Col(1)
   If P1#=-1
      Boom 
      P1_SHIP_Y_POS=170
   End If 
   
   
   
   ' Joysticks and stuff
   If Jup(1)
      P2_SHIP_Y_POS=P2_SHIP_Y_POS-SHIP_V_SPEED
   End If 
   
   If Jup(0)
      P1_SHIP_Y_POS=P1_SHIP_Y_POS-SHIP_V_SPEED
   End If 
   
   
   If Jdown(1)
      P2_SHIP_Y_POS=P2_SHIP_Y_POS+SHIP_V_SPEED
   End If 
   
   If Jdown(0)
      P1_SHIP_Y_POS=P1_SHIP_Y_POS+SHIP_V_SPEED
   End If 
   
   ' This bit optional... add in later... 
   ' Ship inertia...
   
   ' Ship to descend using gravity if no joystick control 
   '   If SHIP_Y_POS<160
   '      SHIP_Y_POS=SHIP_Y_POS+SHIP_I_SPEED
   '   End If 
   
   ' Made it to the top of the screen?
   If P1_SHIP_Y_POS=0
      Inc P1_SCORE
      
      P1_SCORE$=Str$(P1_SCORE)
      
      SCORE_DISPLAY[P1_SCORE$,P2_SCORE$]
      
      P1_SHIP_Y_POS=170
   End If 
   
   If P2_SHIP_Y_POS=0
      Inc P2_SCORE
      
      P2_SCORE$=Str$(P2_SCORE)
      
      SCORE_DISPLAY[P1_SCORE$,P2_SCORE$]
      
      P2_SHIP_Y_POS=170
   End If 
   
   ' Prevent the ships from dropping below the screen   
   If P1_SHIP_Y_POS>=170
      
      P1_SHIP_Y_POS=170
      
   End If 
   
   If P2_SHIP_Y_POS>=170
      
      P2_SHIP_Y_POS=170
      
   End If 
   
   
   ' Check the current time now, against the current iime set at the start  
   ' of this loop 
   
   CT2=Current Time
   
   ' Extract the second and minute values   
   CT2_S=Ct Second(CT2)
   CT2_M=Ct Minute(CT2)
   
   
   ' Subtract the seconds and minutes between CT1 and CT2 
   SEC_DIFF=CT2_S-CT1_S
   MNU_DIFF=CT2_M-CT1_M
   
   
   ' If 1 or more seconds has passed, and 110 instances this
   ' have occurred, Handle the Timer  
   ' Why 110 instances? 
   ' Because this will give us 1 minute 30 secods of game play (approx)   
   
   If SEC_DIFF>=1
      
      Inc SEC_COUNTER
      
      ' If we've hit the 110th time the difference has been 1 second,  
      ' Handle Timer.. 
      If SEC_COUNTER=110
         
         HANDLE_TIMER
         CT1=CT2
         SEC_COUNTER=0
      End If 
   End If 
   
   ' If the difference in seconds is affected by the minutes changing 
   '... check for minute difference being 1 or more.
   ' ... if so, continue as normal. 
   If SEC_DIFF<=0
      
      
      If MNU_DIFF>=1
         Inc SEC_COUNTER
         ' If we've hit the 110th time the difference has been 1 second,  
         ' Handle Timer.. 
         If SEC_COUNTER=110
            HANDLE_TIMER
            CT1=CT2
            SEC_COUNTER=0
         End If 
      End If 
   End If 
   
Loop 


End 

'******************************************************
'* 
'* Handle meteors moving from the left of the screen to
'* the right, and right to the left  
'* 
'******************************************************

Procedure METEORS
   
   ' Bob 4 is our meteor bob    
   For M_COUNT=1 To 13
      
      Bob L_METEOR(M_COUNT),L_X_POS(M_COUNT),L_Y_POS(M_COUNT),4
      L_X_POS(M_COUNT)=L_X_POS(M_COUNT)+M_SPEED
      
      If L_X_POS(M_COUNT)>=300
         
         ' ... how about regenerating the random number it is based on? 
         L_X_POS(M_COUNT)=Rnd(300)
         
      End If 
      
      Bob R_METEOR(M_COUNT),R_X_POS(M_COUNT),R_Y_POS(M_COUNT),4
      R_X_POS(M_COUNT)=R_X_POS(M_COUNT)-M_SPEED
      
      
      If R_X_POS(M_COUNT)<=0
         
         ' ... how about regenerating the random number it is based on? 
         R_X_POS(M_COUNT)=Rnd(300)
         
      End If 
      
   Next M_COUNT
   
End Proc


'******************************************************
'* 
'* Handle the timer bar
'* 
'* Move the block blob down the timer bar
'*      TIM_Y_POS moves down 5 pixels each time this 
'*      proc is called 
'* 
'******************************************************
Procedure HANDLE_TIMER
   
   TIM_Y_POS=TIM_Y_POS+5
   
   Bob 32,160,TIM_Y_POS,6
   
End Proc


'******************************************************
'* 
'* Game over procedure 
'* 
'* Program ends after keypress...  
'* 
'******************************************************
Procedure GAME_OVER
   
   
   Ink 2,0
   Text 80,50,"Game Over!"
   
   ' Test code
   ' Locate 0,3 : Print Ct Time$(Current Time)
   
   Wait Key 

   End 
   
End Proc


'******************************************************
'* 
'* Display player scores 
'* 
'* Params: Player 1 score, Player 2 score
'* 
'******************************************************

Procedure SCORE_DISPLAY[P1_SCORE$,P2_SCORE$]
   
   Ink 2,0
   
   Text 15,190,P1_SCORE$
   Text 260,190,P2_SCORE$
   
End Proc
