Rem ****************************************************
Rem ***                     GAME                     ***
Rem ***    My battery is low and it's getting dark   ***
Rem ***                                              ***
Rem ***               by Alice Massart               ***
Rem ***                 ESAD ORLEANS                 ***
Rem ***               3e annee DVG DAD               ***
Rem ***                                              ***
Rem ***                Decembre 2025                 ***
Rem ****************************************************
Rem ***                                              ***
Rem ***                 AMOS Pro 2.0                 ***
Rem ***            Realise pour Amiga 500+           ***
Rem ****************************************************

Rem **************************
Rem *** Variables globales ***
Rem **************************

Global RoverX,RoverY,ImageID,PupActive
Global YearCounter,T0,GameEnded,ZDelay

RoverX=50
RoverY=50
ImageID=2
PupActive=0
YearCounter=2003
T0=0
GameEnded=0

Rem **************************
Rem *** Parametres de base ***
Rem **************************

Screen Open 0,320,200,16,Lowres
Curs Off
Flash Off
Cls 0
Hide On

Load Iff "dh1:MyBatteryIsLow/SORTIE/MyBatteryIsLow_Game/visuels/decors/fond_Gexp_Xexp.iff",0

Set Sprite Buffer 256

Rem --- Palette BOB personnalisee ---
Get Sprite Palette 0
Colour 0,$000
Colour 1,$111
Colour 2,$333
Colour 3,$333
Colour 4,$640
Colour 5,$555
Colour 6,$666
Colour 7,$960
Colour 8,$898
Colour 9,$C80
Colour 10,$999
Colour 11,$FA0
Colour 12,$FA0
Colour 13,$FB4
Colour 14,$CCC
Colour 15,$FFF

Bob 1,RoverX,RoverY,ImageID

Rem **************************************
Rem ***       Zones de la carte        ***
Rem **************************************

Reserve Zone 10
Rem - Roche -
Set Zone 1,60,140 To 110,168
Rem - Dunes -
Set Zone 2,216,175 To 300,195
Rem - Cratere -
Set Zone 3,256,39 To 310,47
Rem - Epave -
Set Zone 4,280,50 To 330,72
Rem - Meteorite -
Set Zone 5,57,13 To 100,30

Rem **************************
Rem *** Boucle principale  ***
Rem **************************

Screen Open 1,320,256,16,Lowres
Print " fleches directionnelles : deplacer le rover"
Print " ESC : quitter le jeu"
Print " ENTREE : Continuer"
Wait 300
Screen Close 1

Pen 15 : Locate 17,1 : Print YearCounter ;" "

While True
    Wait Vbl

    Rem *** COMPTEUR 3 SECONDES ***
    If T0=0
        T0=Timer
    End If
    
    If Timer-T0>100
        T0=Timer
        YearCounter=YearCounter+1
        Pen 15 : Locate 17,1 : Print YearCounter ;" "
    End If
    If YearCounter=2018
        Screen Close 0

        Screen Open 6,320,256,16,Lowres
        Load Iff "dh1:MyBatteryIsLow/SORTIE/MyBatteryIsLow_Game/visuels/objets/fin/fin_Kexp_Gexp_Xexp.iff",6
        Pen 15
        Paper 0
        Locate 10,20
        Print "'My Battery is low"
        Locate 8,21
        Print "and it's getting dark.'"
        Wait 300
        Screen Close 6

        Screen Open 6,320,256,16,Lowres
        Cls 0
        Pen 2
        Paper 0

        Locate 0,5
        Print "A partir de juin 2018, une tempete de "
        Print "poussiere martienne exceptionnelle prive "
        Print "le rover Opportunity d'energie solaire, "
        Print "le forcant a se mettre en veille"
        Print "profonde. Malgre des mois de tentatives "
        Print "de reveil, il ne repondra jamais."
        
        Locate 0,17
        Print "Le 13 fevrier 2019, la NASA declare"
        Print "officiellement la mission terminee, "
        Print "Opportunity laissant le record "
        Print "de 45,16km parcourus sur un autre monde."
        Wait 1000

        Screen Close 6

        Screen Open 6,320,256,16,Lowres
        Cls 0
        Pen 2
        Paper 0
        Locate 11,5
        Print "Merci d'avoir joue !"
        Locate 6,6
        Print "Jeu realise par Alice Massart"
        Wait 100

        End
    End If

    Rem *** Decremente le delai anti-coincement (ZDelay) ***
    If ZDelay>0
        ZDelay=ZDelay-1
    End If

    Rem --- Si popup ou fin ---
    If PupActive Or GameEnded
        If GameEnded
            If Key State(0)
                End
            End If
        Else
            If Key State(68)
                If PupActive=1
                    Screen Close 1
                End If
                If PupActive=2
                    Screen Close 2
                End If
                If PupActive=3
                    Screen Close 3
                End If
                If PupActive=4
                    Screen Close 4
                End If
                If PupActive=5
                    Screen Close 5
                End If
                PupActive=0
            End If
        End If
        Goto SkipGameplay
    End If

    Rem --- Deplacement ---
    NewX=RoverX
    NewY=RoverY
    NewImageID=ImageID

    If Key State(79)
        NewX=RoverX-2
        NewImageID=2
    Else If Key State(78)
        NewX=RoverX+2
        NewImageID=1
    Else If Key State(76)
        NewY=RoverY-2
    Else If Key State(77)
        NewY=RoverY+2
    Else If Key State(69)
        End
    End If

    Rem --- Collisions ---
    Ztest=Zone(NewX+16,NewY+16)
    If Ztest>=0 And Ztest<6
        RoverX=NewX
        RoverY=NewY
        ImageID=NewImageID
    End If

    Rem --- Zones interactives ---
    Z=Zone(RoverX+16,RoverY+16)

        If Z=1 And PupActive=0 And ZDelay=0
        ZDelay=100
        Screen Open 1,320,256,16,Lowres
        Load Iff "dh1:MyBatteryIsLow/SORTIE/MyBatteryIsLow_Game/visuels/objets/popup/falaise_Kexp_Gexp_Xexp.iff",1
        Pen 15
        Paper 0
        Locate 3,20
        Print "PHOTO : Falaise Burns dans le cratere Endurance"
        Paper 1
        PupActive=1
        Goto SkipGameplay
    End If

    If Z=2 And PupActive=0 And ZDelay=0
        ZDelay=100
        Screen Open 2,320,256,16,Lowres
        Load Iff "dh1:MyBatteryIsLow/SORTIE/MyBatteryIsLow_Game/visuels/objets/popup/dunes_Kexp_Gexp_Xexp.iff",2
        Pen 15
        Paper 0
        Locate 3,20
        Print "PHOTO : Dunes"
        Paper 1
        PupActive=2
        Goto SkipGameplay
    End If

    If Z=3 And PupActive=0 And ZDelay=0
        ZDelay=100
        Screen Open 3,320,256,16,Lowres
        Load Iff "dh1:MyBatteryIsLow/SORTIE/MyBatteryIsLow_Game/visuels/objets/popup/victoria_Kexp_Gexp_Xexp.iff",3
        Pen 15
        Paper 0
        Locate 3,20
        Print "PHOTO : Cratere Victoria (octobre 2006)"
        Paper 1
        PupActive=3
        Goto SkipGameplay
    End If

    If Z=4 And PupActive=0 And ZDelay=0
        ZDelay=100
        Screen Open 4,320,256,16,Lowres
        Load Iff "dh1:MyBatteryIsLow/SORTIE/MyBatteryIsLow_Game/visuels/objets/popup/epave/epave.iff",4
        Pen 15
        Paper 0
        Locate 3,20
        Print "PHOTO : Epave du bouclier thermique, au sud d'Endurance"
        Paper 1
        PupActive=4
        Goto SkipGameplay
    End If

    If Z=5 And PupActive=0 And ZDelay=0
        ZDelay=100
        Screen Open 5,320,256,16,Lowres
        Load Iff "dh1:MyBatteryIsLow/SORTIE/MyBatteryIsLow_Game/visuels/objets/popup/meteorite/meteorite_Kexp_Gexp_Xexp.iff",5
        Pen 15
        Paper 0
        Locate 3,20
        Print "PHOTO : Meteorite Heat Shield Rock"
        Paper 1
        PupActive=5
        Goto SkipGameplay
    End If

    SkipGameplay:
    Bob Update Off
    Bob 1,RoverX,RoverY,ImageID
    Bob Update
Wend

End
